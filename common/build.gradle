import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

buildscript {
    ext.kotlin_version = '1.4.10'
    ext.ktor_version = '1.4.0'
    ext.serialization_version = '1.0.0-RC'
    ext.sl4j_version = '1.6.1'
    ext.exposed_version = '0.24.1'
    ext.buildkonfig_version = '0.7.0'
    ext.flyway_version = '6.5.6'
    ext.mysql_connector_version = '8.0.19'
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.2.0'
        classpath 'mysql:mysql-connector-java:8.0.12'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
        classpath "com.codingfeline.buildkonfig:buildkonfig-gradle-plugin:$buildkonfig_version"
    }
}

plugins {
    id 'org.jetbrains.kotlin.multiplatform' version "$kotlin_version"
    id "org.flywaydb.flyway" version "$flyway_version"
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def dbEndpoint = properties.getProperty('database.endpoint')
def dbUser = properties.getProperty('database.user')
def dbPassword = properties.getProperty('database.password')
def dbDatabase = properties.getProperty('database.database')

def projectPackageName = "com.marzec.fiteo"

apply plugin: 'kotlinx-serialization'
apply plugin: 'application'
apply plugin: 'com.codingfeline.buildkonfig'

mainClassName = 'com.marzec.JvmMainKt'

apply plugin: 'com.github.johnrengelman.shadow'

repositories {
    mavenCentral()
    jcenter()
}
group projectPackageName
version '0.0.1'

apply plugin: 'maven-publish'

kotlin {

    jvm {

    }
    js {
        browser {
        }
        nodejs {
        }
    }
    // For ARM, should be changed to iosArm32 or iosArm64
    // For Linux, should be changed to e.g. linuxX64
    // For MacOS, should be changed to e.g. macosX64
    // For Windows, should be changed to e.g. mingwX64
    mingwX64("mingw") {
        binaries {
            executable {
                // Change to specify fully qualified name of your application's entry point:
                entryPoint = 'main'
                // Specify command-line arguments, if necessary:
                runTask?.args('')
            }
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-core:$serialization_version"
                implementation "org.jetbrains.exposed:exposed-core:$exposed_version"
                implementation "org.jetbrains.exposed:exposed-dao:$exposed_version"
                implementation "org.jetbrains.exposed:exposed-jdbc:$exposed_version"
                implementation "mysql:mysql-connector-java:$mysql_connector_version"
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')
                implementation "io.ktor:ktor-html-builder:$ktor_version"
                implementation "io.ktor:ktor-server-sessions:$ktor_version"
                implementation "io.ktor:ktor-serialization:$ktor_version"
                implementation "io.ktor:ktor-auth:$ktor_version"
                implementation "io.ktor:ktor-server-netty:$ktor_version"
                implementation "org.slf4j:slf4j-simple:$sl4j_version"

            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
            }
        }
        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')
            }
        }
        jsTest {
            dependencies {
                implementation kotlin('test-js')
            }
        }
        mingwMain {
            dependencies {
            }
        }
        mingwTest {
        }
    }
}

task createfatJar(type: ShadowJar) {
    def target = kotlin.targets.jvm
    from target.compilations.main.output
    def runtimeClasspath = target.compilations.main.runtimeDependencyFiles
    manifest {
        attributes 'Main-Class': mainClassName
    }
    configurations = [runtimeClasspath]
}

flyway {
    url = dbEndpoint
    user = dbUser
    password = dbPassword
    schemas = [dbDatabase]
    createSchemas=true
    placeholders = [
            'database_name': dbDatabase
    ]
    locations = [
            'filesystem:/' + projectDir.toString() + '/src/commonMain/resources/db/migration'
    ]
}

buildkonfig {
    packageName projectPackageName
    defaultConfigs {
        buildConfigField 'String', 'DB_ENDPOINT', dbEndpoint
        buildConfigField 'String', 'DB_USER', dbUser
        buildConfigField 'String', 'DB_PASSWORD', dbPassword
        buildConfigField 'String', 'DB_DATABASE', dbDatabase
    }
}